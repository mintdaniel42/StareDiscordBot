plugins {
    id('java')
    id('application')
    alias(libs.plugins.lombok)
    alias(libs.plugins.buildConfig)
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation(libs.jda)
    implementation(libs.ormlite)
    implementation(libs.jdbc.sqlite)
    implementation(libs.json)
    implementation(libs.slf4j.api)
    implementation(libs.slf4j.simple)
    annotationProcessor(libs.manifold.preprocessor)
}

group = 'org.mintdaniel42.starediscordbot'
version = '1.0-SNAPSHOT'
application.mainClass = "${project.group}.Bot"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    manifest.attributes("Main-Class": application.mainClass)

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it)}
    }

    exclude("org/mintdaniel42/starediscordbot/build/**")
}

tasks.compileJava {
    options.compilerArgs += ['-Xplugin:Manifold']
    if (System.getenv("DEV") == "1") options.compilerArgs += ['-Adev']
}

buildConfig {
    packageName("${project.group}.build")
    forClass(className = "BuildConfig") {
        buildConfigField(boolean, "dev", System.getenv("DEV") == "1")
        buildConfigField(long, "cleaningInterval", 300 * 1_000) // 300 seconds
        buildConfigField(long, "maxRequestAge", 2 * 1_000 * 86400) // 2 days
        buildConfigField(long, "maxUsernameAge", 5 * 1_000 * 86400) // 5 days
        buildConfigField(byte, "entriesPerPage", 10)
        buildConfigField("java.util.Locale", "locale", "Locale.GERMANY")
    }
}